import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'org.flywaydb.flyway' version "8.0.0"
}

group 'telraam'
version '1.0-SNAPSHOT'

sourceCompatibility = 16

// Set our project variables
project.ext {
    dropwizardVersion = '2.0.25'
}

repositories {
    mavenCentral()
}
application {
    mainClass.set('telraam.App')
}

task runDev {

    finalizedBy {
        run.environment("CONFIG_KEY", "DEVELOPMENT")
        run.args('server', "$rootProject.projectDir/src/main/resources/telraam/devConfig.yml")
        run
    }
}
task runProd {

    finalizedBy {
        run.environment("CONFIG_KEY", "PRODUCTION")
        run.args('server', "$rootProject.projectDir/src/main/resources/telraam/prodConfig.yml")
        run
    }
}
idea {
    module {
        inheritOutputDirs = true
    }
}
build.finalizedBy(javadoc)

dependencies {
    // Web framework stuff
    implementation(
            'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
            'io.dropwizard:dropwizard-auth:' + dropwizardVersion,
            'io.dropwizard:dropwizard-jdbi3:' + dropwizardVersion,
    )
    // Database
    implementation('com.h2database:h2:1.4.200')
    implementation('org.postgresql:postgresql:42.2.24.jre7')

    // Testing
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.1')
    testImplementation('org.flywaydb:flyway-core:7.14.1')
    testImplementation('org.mockito:mockito-core:3.12.4')
    testImplementation("io.dropwizard:dropwizard-testing:" + dropwizardVersion)

    // Statistics for Viterbi-lapper
    implementation("org.apache.commons:commons-math3:3.0")

    // JAX-B dependencies for JDK 9+  -> https://stackoverflow.com/a/43574427
    implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
    implementation 'org.glassfish.jaxb:jaxb-runtime:3.0.1'

    // Swagger-UI
    implementation 'com.smoketurner:dropwizard-swagger:2.0.0-1'
}

test {
    environment("CONFIG_KEY", "TESTING")
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

def prodProps = new Properties()
file("$rootProject.projectDir/src/main/resources/telraam/prodConfig.properties").withInputStream {
    prodProps.load(it)
}
task migrateProductionDatabase(type: FlywayMigrateTask) {
    url = prodProps.getProperty("DB_URL")
}

def devProps = new Properties()
file("$rootProject.projectDir/src/main/resources/telraam/devConfig.properties").withInputStream {
    devProps.load(it)
}
task migrateDevelopmentDatabase(type: FlywayMigrateTask) {
    url = devProps.getProperty("DB_URL")
    user = devProps.getProperty("DB_USER")
    password = devProps.getProperty("DB_PASSWORD")
}

def testProps = new Properties()
file("$rootProject.projectDir/src/test/resources/telraam/testConfig.properties").withInputStream {
    testProps.load(it)
}
task migrateTestingDatabase(type: FlywayMigrateTask) {
    url = testProps.getProperty("DB_URL")
    baselineOnMigrate = true
}
