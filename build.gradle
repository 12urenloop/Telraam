import org.flywaydb.gradle.task.FlywayMigrateTask

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'org.flywaydb.flyway' version "10.0.0"
}

group 'telraam'
version '1.0-SNAPSHOT'

sourceCompatibility = 17

// Set our project variables
project.ext {
    dropwizardVersion = '4.0.5'
}

repositories {
    mavenCentral()
}
application {
    mainClass.set('telraam.App')
}

tasks.register('runDev') {

    finalizedBy {
        run.environment("CONFIG_KEY", "DEVELOPMENT")
        run.args('server', "$rootProject.projectDir/src/main/resources/telraam/devConfig.yml")
        run
    }
}
tasks.register('runProd') {

    finalizedBy {
        run.environment("CONFIG_KEY", "PRODUCTION")
        run.args('server', "$rootProject.projectDir/src/main/resources/telraam/prodConfig.yml")
        run
    }
}
idea {
    module {
        inheritOutputDirs = true
    }
}
build.finalizedBy(javadoc)

dependencies {
    // Web framework stuff
    implementation(
            'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
            'io.dropwizard:dropwizard-auth:' + dropwizardVersion,
            'io.dropwizard:dropwizard-jdbi3:' + dropwizardVersion,
    )
    // Database
    implementation('com.h2database:h2:2.2.220')
    implementation('org.postgresql:postgresql:42.7.3')

    // Testing
    testImplementation('org.junit.jupiter:junit-jupiter:5.10.2')
    testImplementation('org.flywaydb:flyway-core:10.10.0')
    testImplementation('org.mockito:mockito-core:5.11.0')
    testImplementation("io.dropwizard:dropwizard-testing:" + dropwizardVersion)

    // Statistics for Viterbi-lapper
    implementation("org.apache.commons:commons-math3:3.6.1")

    // JAX-B dependencies for JDK 9+  -> https://stackoverflow.com/a/43574427
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    implementation 'org.glassfish.jaxb:jaxb-runtime:4.0.5'

    // Swagger-UI
    implementation 'com.smoketurner:dropwizard-swagger:4.0.5-1'
}

test {
    environment("CONFIG_KEY", "TESTING")
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

def prodProps = new Properties()
file("$rootProject.projectDir/src/main/resources/telraam/prodConfig.properties").withInputStream {
    prodProps.load(it)
}
tasks.register('migrateProductionDatabase', FlywayMigrateTask) {
    url = prodProps.getProperty("DB_URL")
}

def devProps = new Properties()
file("$rootProject.projectDir/src/main/resources/telraam/devConfig.properties").withInputStream {
    devProps.load(it)
}
tasks.register('migrateDevelopmentDatabase', FlywayMigrateTask) {
    url = devProps.getProperty("DB_URL")
    user = devProps.getProperty("DB_USER")
    password = devProps.getProperty("DB_PASSWORD")
}

def testProps = new Properties()
file("$rootProject.projectDir/src/test/resources/telraam/testConfig.properties").withInputStream {
    testProps.load(it)
}
tasks.register('migrateTestingDatabase', FlywayMigrateTask) {
    url = testProps.getProperty("DB_URL")
    baselineOnMigrate = true
}